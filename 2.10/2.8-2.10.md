### 2.8

#### Hello world

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform float u_time;

void main() {
	gl_FragColor = vec4(0.000,1.000,0.000,0.800);
}

```

tips:

1. ​	shader 语言 有一个 `main` 函数，会在最后返回颜色值。 最终的像素颜色取决于预设的全局变量 `gl_FragColor`。
2. ​    这个类 C 语言有内建的**变量**（像`gl_FragColor`），**函数**和**数据类型**。  `vec4`（四分量浮点向量）`vec3` （三分量浮点向量）和 `vec2` （二分量浮点向量），还有非常著名的：`float`（单精度浮点型）， `int`（整型） 和 `bool`（布尔型）。
3. ​     vec4类型 rgba  响应红，绿，蓝和透明度  我们也可以看到这些变量是**规范化**的，意思是它们的值是从0到1的
4. ​     预处理程序的宏指令。 有了宏才可以 `#define` （定义）全局变量和进行一些基础的条件运算（通过使用 `#ifdef` 和 `#endif`）。所有的宏都以 `#` 开头。预编译会在编译前一刻发生，把所有的命令复制到 `#defines` 里，检查`#ifdef` 条件句是否已被定义， `#ifndef` 条件句是否没有被定义。在我们刚刚的“hello world!”的例子中，我们在第2行检查了 `GL_ES` 是否被定义，这个通常用在移动端或浏览器的编译中。
5. ​    `float`类型在 shaders 中非常重要，所以**精度**非常重要。**更低的精度会有更快的渲染速度，但是会以质量为代价。**你可以选择每一个浮点值的精度。在第一行（`precision mediump float;`）我们就是设定了所有的浮点值都是中等精度。但我们也可以选择把这个值设为“低”（`precision lowp float;`）或者“高”（`precision highp float;`）。
6. ​    GLSL 语言规范并不保证变量会被自动转换类别。 显卡的硬件制造商各有不同的显卡加速方式，但是却被要求有最精简的语言规范。因而，自动强制类型转换并没有包括在其中。在我们的“hello world!”例子中，`vec4` 精确到单精度浮点，所以应被赋予 `float` 格式。但是如果你想要代码前后一致，不要之后花费大量时间 debug 的话，最好养成在 `float` 型数值里加一个 `.` 的好习惯。

​		  `gl_FragColor = vec4(1,0,0,1)` 有可能会出错

<img src="https://first-1303075678.cos.ap-beijing.myqcloud.com/img/202203191444568.png" alt="image-20220208150042966" style="zoom:50%;" />



- 容忍 
- 空白
- 空白
- 空白

#### Uniforms(统一值)

因为显卡的架构，所有线程的输入值必须**统一**（uniform），而且必须设为**只读**。也就是说，每条线程接收相同的数据，并且是不可改变的数据。

它们的数据类型通常为：`float`, `vec2`, `vec3`, `vec4`, `mat2`, `mat3`, `mat4`, `sampler2D` and `samplerCube`。uniform 值需要数值类型前后一致。且在 shader 的开头，在设定精度之后，就对其进行定义。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution; // 画布尺寸（宽，高）
uniform vec2 u_mouse;      // 鼠标位置（在屏幕上哪个像素）
uniform float u_time;     // 时间（加载后的秒数）
```

u_time (时间) u_resolution (画布尺寸) u_mouse (鼠标位置)

业界传统在 uniform 值前加 **u_**

也有例外 [ShaderToy.com](https://www.shadertoy.com/)就用了如下的名字

```glsl
uniform vec3 iResolution;   // 视口分辨率（以像素计）
uniform vec4 iMouse;        // 鼠标坐标 xy： 当前位置, zw： 点击位置
uniform float iTime;        // shader 运行时间（以秒计）
```



红色动态变化

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform float u_time;

void main() {
	gl_FragColor = vec4(abs(sin(u_time)),0.0,0.0,1.0);
}
```

(red -> black -> red -> black )

其他函数

[`sin()`](https://thebookofshaders.com/glossary/?search=sin), [`cos()`](https://thebookofshaders.com/glossary/?search=cos), [`tan()`](https://thebookofshaders.com/glossary/?search=tan), [`asin()`](https://thebookofshaders.com/glossary/?search=asin), [`acos()`](https://thebookofshaders.com/glossary/?search=acos), [`atan()`](https://thebookofshaders.com/glossary/?search=atan), [`pow()`](https://thebookofshaders.com/glossary/?search=pow), [`exp()`](https://thebookofshaders.com/glossary/?search=exp), [`log()`](https://thebookofshaders.com/glossary/?search=log), [`sqrt()`](https://thebookofshaders.com/glossary/?search=sqrt), [`abs()`](https://thebookofshaders.com/glossary/?search=abs), [`sign()`](https://thebookofshaders.com/glossary/?search=sign), [`floor()`](https://thebookofshaders.com/glossary/?search=floor), [`ceil()`](https://thebookofshaders.com/glossary/?search=ceil), [`fract()`](https://thebookofshaders.com/glossary/?search=fract), [`mod()`](https://thebookofshaders.com/glossary/?search=mod), [`min()`](https://thebookofshaders.com/glossary/?search=min), [`max()`](https://thebookofshaders.com/glossary/?search=max) 和 [`clamp()`](https://thebookofshaders.com/glossary/?search=clamp)。



大多数原生函数都是硬件加速的，也就是说如果你正确使用这些函数，你的代码就会跑得更快。



#### gl_FragCoord(varying)

就像 GLSL 有个默认输出值 `vec4 gl_FragColor` 一样，它也有一个默认输入值（ `vec4 gl_FragCoord` ）

`gl_FragCoord`存储了活动线程正在处理的**像素**或**屏幕碎片**的坐标。有了它我们就知道了屏幕上的哪一个线程正在运转。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

void main() {
	vec2 st = gl_FragCoord.xy/u_resolution;
	gl_FragColor = vec4(st.x,st.y,0.7,1.0);
}
```

![image-20220208220923622](https://first-1303075678.cos.ap-beijing.myqcloud.com/img/202203191444521.png)

上述代码中我们用 `gl_FragCoord.xy` 除以 `u_resolution`，对坐标进行了**规范化**。这样做是为了使所有的值落在 `0.0` 到 `1.0` 之间，这样就可以轻松把 X 或 Y 的值映射到红色或者绿色通道。



### 2.10(Algorithmic drawing)

#### Shaping functions

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

// Plot a line on Y using a value between 0.0-1.0
float plot(vec2 st) {    
    return smoothstep(0.02, 0.0, abs(st.y - st.x));
}

void main() {
	vec2 st = gl_FragCoord.xy/u_resolution;

    float y = st.x;

    vec3 color = vec3(y);

    // Plot a line
    float pct = plot(st);
    color = (1.0-pct)*color+pct*vec3(0.0,1.0,0.0);

	gl_FragColor = vec4(color,1.0);
}

```

`vec3` 类型构造器“明白”你想要把一个值赋值到颜色的三个通道里，就像 `vec4` 明白你想要构建一个四维向量，三维向量加上第四个值（比如颜色的三个值加上透明度）

这些 x 与 y（或亮度）之间一对一的关系称作**线性插值**（linear interpolation）。

(插值是离散函数逼近的重要方法，利用它可通过函数在有限个点处的取值状况，估算出函数在其他点处的近似值。因为对计算机来说，屏幕像素是离散的而不是连续的，计算机图形学常用插值来填充图像像素之间的空隙。)



#### Step && SmoothStep

原生插值函数可以被硬件加速。

[`step()`](https://thebookofshaders.com/glossary/?search=step) 插值函数需要输入两个参数。第一个是极限或阈值，第二个是我们想要检测或通过的值。对任何小于阈值的值，返回 `0.0`，大于阈值，则返回 `1.0`。

另一个 GLSL 的特殊函数是 [`smoothstep()`](https://thebookofshaders.com/glossary/?search=smoothstep)。当给定一个范围的上下限和一个数值，这个函数会在已有的范围内给出插值。前两个参数规定转换的开始和结束点，第三个是给出一个值用来插值。



```glsl
#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.14159265359

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

float plot(vec2 st, float pct){
  return  smoothstep( pct-0.02, pct, st.y) -
          smoothstep( pct, pct+0.02, st.y);
}

void main() {
    vec2 st = gl_FragCoord.xy/u_resolution;

    // Step will return 0.0 unless the value is over 0.5,
    // in that case it will return 1.0
    float y = step(0.5,st.x);
    
    // Smooth interpolation between 0.1 and 0.9
    float y = smoothstep(0.1,0.9,st.x);

    vec3 color = vec3(y);

    float pct = plot(st,y);
    color = (1.0-pct)*color+pct*vec3(0.0,1.0,0.0);

    gl_FragColor = vec4(color,1.0);
}
```

<img src="https://thebookofshaders.com/05/kynd.png" alt="img" style="zoom: 200%;" />



#### Colors

描述颜色的时候要尽可能地满足以下 2 个原则：

人眼看到的色差 = 颜色向量间的欧氏距离

相同的亮度，能让人感觉亮度相同

##### RGBA

定义颜色 (rgba)

```glsl
vec3 red = vec3(1.0,0.0,0.0);
red.x = 1.0;
red.y = 0.0;
red.z = 0.0;

vec4 vector;
vector[0] = vector.r = vector.x = vector.s;
vector[1] = vector.g = vector.y = vector.t;
vector[2] = vector.b = vector.z = vector.p;
vector[3] = vector.a = vector.w = vector.q;
```

这些指向向量内部变量的不同方式仅仅是设计用来帮助你写出干净代码的术语。着色语言所包含的灵活性为你互换地思考颜色和坐标位置。

GLSL中向量类型的另一大特点是可以用你需要的任意顺序简单地投射和混合（变量）值。这种能力被（形象地）称为：**鸡尾酒**。(swizzle)

```glsl
vec3 yellow, magenta, green;

// Making Yellow
yellow.rg = vec2(1.0);  // Assigning 1. to red and green channels
yellow[2] = 0.0;        // Assigning 0. to blue channel

// Making Magenta(品红)
magenta = yellow.rbg;   // Assign the channels with green and blue swapped

// Making Green
green.rgb = yellow.bgb; // Assign the blue channel of Yellow (0) to red and blue channels
```



##### HSB/L/V (Hue, Saturation, Brightness / value)

[HSB](http://en.wikipedia.org/wiki/HSL_and_HSV) 代表色相，饱和度和亮度（或称为值）。这更符合直觉也更有利于组织颜色。

Hue 是角度，取值范围是 0 到 360 度，饱和度和亮度 / 明度的值都是从 0 到 100%。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform float u_time;

// hsb 转换 rgb
vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix(vec3(1.0), rgb, c.y);
}

void main(){
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    // We map x (0.0 - 1.0) to the hue (0.0 - 1.0)
    // And the y (0.0 - 1.0) to the brightness
    color = hsb2rgb(vec3(st.x,1.0,st.y));

    gl_FragColor = vec4(color,1.0);
}

```



##### rgb2hsb

```glsl
vec3 rgb2hsb( in vec3 c ){
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz),
                 vec4(c.gb, K.xy),
                 step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r),
                 vec4(c.r, p.yzx),
                 step(p.x, c.r));
    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)),
                d / (q.x + e),
                q.x);
}
```



##### HSB in polar coordinates

<img src="https://first-1303075678.cos.ap-beijing.myqcloud.com/img/202203191444961.jpeg" alt="img" style="zoom: 33%;" />

HSB was originally designed to be represented in polar coordinates (based on the angle and radius)

将HSB映射到极坐标我们需要取得角度和到像素屏中点的距离。由此我们运用 [`length()`](https://thebookofshaders.com/glossary/?search=length) 函数和 [`atan(y,x)`](https://thebookofshaders.com/glossary/?search=atan) 函数（在GLSL中通常用atan（y,x））。

当用到矢量和三角学函数时，`vec2`, `vec3` 和 `vec4`被当做向量对待，即使有时候他们代表颜色。我们开始把颜色和向量同等的对待，事实上你会慢慢发现这种理念的灵活性有着相当强大的用途。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

#define TWO_PI 6.28318530718

uniform vec2 u_resolution;
uniform float u_time;

vec3 hsb2rgb( in vec3 c ){
    vec3 rgb = clamp(abs(mod(c.x*6.0+vec3(0.0,4.0,2.0),
                             6.0)-3.0)-1.0,
                     0.0,
                     1.0 );
    rgb = rgb*rgb*(3.0-2.0*rgb);
    return c.z * mix( vec3(1.0), rgb, c.y);
}

void main(){
    vec2 st = gl_FragCoord.xy/u_resolution;
    vec3 color = vec3(0.0);

    // Use polar coordinates instead of cartesian
    vec2 toCenter = vec2(0.5)-st;
    float angle = atan(toCenter.y,toCenter.x);
    float radius = length(toCenter)*2.0;

    // Map the angle (-PI to PI) to the Hue (from 0 to 1)
    // and the Saturation to the radius
    color = hsb2rgb(vec3((angle/TWO_PI)+0.5,radius,1.0));

    gl_FragColor = vec4(color,1.0);
}

```

一旦我们得到角度和长度，我们需要单位化这些值：0.0到1.0。

在27行，[`atan(y,x)`](https://thebookofshaders.com/glossary/?search=atan) 会返回一个介于-PI到PI的弧度值（-3.14 to 3.14），所以我们要将这个返回值除以 `TWO_PI`（在code顶部定义了）来得到一个-0.5到0.5的值。

这样一来，用简单的加法就可以把这个返回值最终映射到0.0到1.0。

半径会返回一个最大值0.5（因为我们计算的是到视口中心的距离，而视口中心的范围已经被映射到0.0到1.0），所以我们需要把这个值乘以二来得到一个0到1.0的映射。



in && out && inout

```glsl
int newFunction(in vec4 aVec4,   // read-only
                out vec3 aVec3,    // write-only
                inout int aInt);   // read-write
```



##### YUV



[YUV](http://en.wikipedia.org/wiki/YUV) 是个用来模拟照片和视频的编码的色彩空间。这个色彩空间考虑人类的感知，减少色度的带宽。

<img src="https://first-1303075678.cos.ap-beijing.myqcloud.com/img/202203191444601.png" alt="image-20220214133238311" style="zoom:67%;" />



```glsl
// YUV to RGB matrix
mat3 yuv2rgb = mat3(1.0, 0.0, 1.13983,
                    1.0, -0.39465, -0.58060,
                    1.0, 2.03211, 0.0);

// RGB to YUV matrix
mat3 rgb2yuv = mat3(0.2126, 0.7152, 0.0722,
                    -0.09991, -0.33609, 0.43600,
                    0.615, -0.5586, -0.05639);
```



#### Square

想象我们有张数学课上使用的方格纸，而我们的作业是画一个正方形。纸的大小是10 x 10而正方形应该是8 x 8。你会怎么做？

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;

void main () {
	vec2 st = gl_FragCoord.xy / u_resolution;
	vec3 color = vec3(0.0);

  // bottom-left
  vec2 bl = step(vec2(0.1),st);
  float pct = bl.x * bl.y;

  // top-right
  vec2 tr = step(vec2(0.1),1.0-st);
  pct *= tr.x * tr.y;
	// color = vec3(left * bottom);
  color = vec3(pct);

  gl_FragColor = vec4(color,1.0);
}

	// 核心
    vec2 bl = step(vec2(0.1),st);       // bottom-left
    vec2 tr = step(vec2(0.1),1.0-st);   // top-right
    color = vec3(bl.x * bl.y * tr.x * tr.y);
```

#### Circle

在笛卡尔坐标系下，用方格纸来画正方形和长方形是很容易的。但是画圆就需要另一种方式了，尤其我们需要一个对“每个像素”的算法。一种解决办法是用[`step()`](https://thebookofshaders.com/glossary/?search=step)函数将重新映射的空间坐标来画圆。

将这个过程翻译给 shader 意味着纸上的每个方形格点都会隐含着问每个像素（线程）是否在圆的区域以内。我们通过计算像素到中心的距离来实现（这个判断）。

有几种方法来计算距离。最简单的是用[`distance()`](https://thebookofshaders.com/glossary/?search=distance)函数，这个函数其实内部调用 [`length()`](https://thebookofshaders.com/glossary/?search=length)函数，计算不同两点的距离（在此例中是像素坐标和画布中心的距离）。length（）函数内部只不过是用平方根([`sqrt()`](https://thebookofshaders.com/glossary/?search=sqrt))计算斜边的方程。

```glsl
#ifdef GL_ES
precision mediump float;
#endif

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

void main(){
	vec2 st = gl_FragCoord.xy/u_resolution;
    float pct = 0.0;

    // a. The DISTANCE from the pixel to the center
    // pct = distance(st,vec2(0.5));

    // b. The LENGTH of the vector
    //    from the pixel to the center
    vec2 toCenter = vec2(0.5)-st;
    pct = length(toCenter);

    // c. The SQUARE ROOT of the vector
    //    from the pixel to the center
    // vec2 tC = vec2(0.5)-st;
    // pct = sqrt(tC.x*tC.x+tC.y*tC.y);

    vec3 color = vec3(pct);

	gl_FragColor = vec4( color, 1.0 );
}

```

![image-20220212121919282](https://first-1303075678.cos.ap-beijing.myqcloud.com/img/202203191444584.png)



L14 就计算效率而言，[`sqrt()`](https://thebookofshaders.com/glossary/?search=sqrt)函数，以及所有依赖它的运算，都耗时耗力。[`dot()`](https://thebookofshaders.com/glossary/?search=dot)点乘是另外一种用来高效计算圆形距离场的方式。

